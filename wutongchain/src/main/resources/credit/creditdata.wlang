contract CreditDataContract {
string identityContractAddr  = "a4b2d9d61b9504541de92cc0f47b44b88a0d2332cf85dad469ea2a9e5da94650"
    struct CreditData {
        string EnterpriseName //授权公司名称
        string EnterpriseCode //授权公司Code
        string CreditName     //征信公司名称
        string CreditCode     //征信公司ID
        string Hash           //原始征信数据摘要
        string Catalogue      //征信数据大分类信息
        int64  MakeTime       //原始征信数据生成时间戳
        string AccessInterface//获取原始征信数据URL
        string Description    //额外备注或者描述
        string Sender         //上链者公钥
        int64  TxTime         //上链时间
        string TxHash         //上链交易hash
        int64 BlockNo        //交易区块链信息
    }

    struct CreditDataList{
        CreditData[] CreditDataList
    }

    //添加征信报告
    public string AddCreditDataList(string json,string sign,string code){
        string args = "[\""+code+"\"]"
        //根据征信企业code获取pubkey
        string AdminPubKey = invoke(identityContractAddr,"GetPubKeyByCode",args)
		
		AdminPubKey = base64ToString(AdminPubKey)
        if(verify("sm2", AdminPubKey, json, sign) != true){
            string tempErr = "Verify fail, please check sign info"
            print(tempErr)
            return tempErr
        }
        int64 timeNow = getBlockTime()
        string txid = getTxID()
        string pubkey = getTxSender()
        int64 height = getBlockHeight()
        CreditDataList cdList = json_to_obj<CreditDataList>(json)
        CreditData[] creditDataList = cdList.CreditDataList
        int loop = len(creditDataList)
        for(int i=0; i<loop; i++){
            CreditData cdata = creditDataList[i]
            cdata.TxTime = timeNow
            cdata.TxHash = txid
            cdata.Sender = pubkey
            cdata.BlockNo = height
            string tmpJson = obj_to_json(cdata)
            string key = "cd_"+cdata.EnterpriseCode+cdata.Hash
            db_set(key,tmpJson)
        }
        json = obj_to_json(creditDataList)
        event("CreditData",json)
        return "success"
    }

    //获取征信报告
    public string GetCreditData(string enterpriseCode,string hash){
        string prefix = "cd_"+enterpriseCode+hash
        
        map<string,string> res
		//获取想要查看的前缀数据，交由过滤器过滤
		db_search(prefix,filter,res)
        string json = ""
		json = obj_to_json(res)
        print("Query Result：\n" + json)
		return json
    }
    //过滤器
	public bool filter(string val){
		return true
	}
	
		public string init(){

        return "success"
    }

}
