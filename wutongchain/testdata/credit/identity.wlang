contract IdentityContract {

    //身份信息结构体
    struct Identity {
        string  Name                //公司名称
        string  Code                //公司Code
        string  Type                //类型
        string  PubKey              //身份公钥(pem)
        string  Address             //地址
        string  ContractAddress     //合约地址
        string  Description         //额外备注或者描述
        int64   TxTime              //上链时间
        string  TxHash              //上链的交易hash
        int64   BlockNo             //区块链信息
    }

    //添加身份信息
    public string AddIdentity(string json){
        int64 timeNow = getBlockTime()
        string txid = getTxID()
        int64 height = getBlockHeight()
        Identity id = json_to_obj<Identity>(json)
        string key = "id_"+id.Code
        id.TxTime = timeNow
        id.TxHash = txid
        id.BlockNo = height
        json = obj_to_json(id)
        db_set(key,json)
        return "success"
    }

    //根据公司Code获取身份公钥
    public string GetPubKeyByCode(string code){
        string key = "id_"+code
        string json = db_get<string>(key)
        if(json==""){
            print("公钥不存在")
            return "error"
        }
        Identity id = json_to_obj<Identity>(json)
        string pubkey = id.PubKey
        return pubkey
    }

    public string GetAllIdentity(){
        string prefix = "id_"
        map<string,string> res
		//获取想要查看的前缀数据，交由过滤器过滤
		db_search(prefix,filter,res)
        string json = ""
		json = obj_to_json(res)
        // print("Query Result：\n" + json)
		return json
    }
    //过滤器
	public bool filter(string val){
		return true
	}

    //根据公司Code获取身份信息
    public string GetIdentity(string code){
        string json = db_get<string>("id_"+code)
        if(json==""){
            print("企业不存在")
            return "error"
        }
        return json
    }
}